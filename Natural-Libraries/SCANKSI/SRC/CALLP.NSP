* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
*************** 
* Display Members CALLed from Workfile 1 
*************** 
DEFINE DATA 
LOCAL 
1 #L-WORKFILE (a255) const <'/home/sag/analyse/sort2.txt'> 
1 #INBUFFER (A300) 
1 REDEFINE #INBUFFER 
  2 #LIB (A8) 
  2 filler 1x 
  2 #PROG (A8) 
  2 filler 1x 
  2 #TYP (A1) 
  2 filler 1x 
  2 #MODE (A1) 
  2 filler 1x 
  2 #CODE (A3) 
  2 filler 1x 
  2 #LINE (A4) 
  2 filler 1x 
  2 #REMARK (A200) 
  2 REDEFINE #REMARK 
    3 #REMARKA (A1/1:200) 
1 #CODECOUNT (I4) 
1 #COUNTCALL (I4) 
1 #CALLARRAY (A32/1:500) 
1 #COUNTARRAY(A32/1:500,1:500) 
1 #MODCOUNTARRAY(I4/1:500) 
1 #I (I4) 
1 #I2 (I4) 
1 #FOUND (L) 
1 #FOUND2 (L) 
1 #CALL (A32) 
1 #POS (I4) 
END-DEFINE 
/* 
define work file 1 #l-workfile 
/* 
READ WORK 1 #INBUFFER 
  IF #CODE NE 'CAL' AND #CODE NE 'CMC' 
    ESCAPE TOP 
  END-IF 
  EXAMINE #REMARK FOR '''' GIVING POSITION #POS 
  IF #POS = 0 
    #POS:=5 
  END-IF 
  RESET #CALL 
  ADD 1 TO #POS 
  #I2 := 0 
  FOR #I = #POS TO 200 
    ADD 1 TO #I2 
    IF #REMARKA(#I) = '''' OR #I2 = 33 
      ESCAPE BOTTOM 
    END-IF 
    COMPRESS #CALL #REMARKA(#I) INTO #CALL LEAVING NO 
  END-FOR 
  MOVE *TRANSLATE(#CALL, UPPER) TO #CALL 
  IF #CALL = ' ' 
    ESCAPE TOP 
  END-IF 
  #FOUND := FALSE 
  FOR #I = 1 TO #COUNTCALL 
    IF #CALLARRAY(#I) = #CALL 
      #FOUND := TRUE 
      #FOUND2 := FALSE 
      FOR #I2 = 1 TO #MODCOUNTARRAY(#I) 
        IF #COUNTARRAY(#I,#I2) = #PROG 
          #FOUND2 := TRUE 
          ESCAPE BOTTOM 
        END-IF 
      END-FOR 
      IF NOT #FOUND2 
         ADD 1 TO #MODCOUNTARRAY(#I) 
         #I2 := #MODCOUNTARRAY(#I) 
         #COUNTARRAY(#I,#I2) := #PROG 
      END-IF 
      ESCAPE BOTTOM 
    END-IF 
  END-FOR 
  IF NOT #FOUND 
    ADD 1 TO #COUNTCALL 
    #CALLARRAY(#COUNTCALL) := #CALL 
    #COUNTARRAY(#COUNTCALL,1) := #PROG 
    #MODCOUNTARRAY(#COUNTCALL) := 1 
  END-IF 
END-WORK 
FOR #I = 1 TO #COUNTCALL 
write (1) #CALLARRAY(#I) #MODCOUNTARRAY(#I) 
**  for #i2 = 1 to #modcountarray(#i) 
**     write '------' #countarray(#i,#i2) 
**  end-for 
END-FOR 
END 
